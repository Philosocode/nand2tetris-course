// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
        // Should load if reset, load, or inc are set
        Or(a=load, b=inc, out=or1);
        Or(a=or1, b=reset, out=shouldLoad);

        // if inc is set, increment registerOut and pass into the register
        Inc16(in=registerOut, out=incOut);

        // if load is set, pass the in directly into the register
        Mux16(a=incOut, b=in, sel=load, out=incOrLoadOut);

        // if reset is set, pass false into the register
        Mux16(a=incOrLoadOut, b=false, sel=reset, out=registerIn);

        Register(in=registerIn, load=shouldLoad, out=out, out=registerOut);
}